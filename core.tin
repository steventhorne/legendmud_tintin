#LINE quiet {#READ sys.config};

#ALIAS {reloadmodule}
{
    #IF {"%1" == "" || "%1" == "all"}
    {
        #SHOWME \nReloading all modules...;
        #LINE quiet {#READ core_module.tin};

        #UNVAR autologin;
        #UNVAR login_name;
        #UNVAR login_password;

        #DELAY {0} {#SHOWME Reload complete.\n};
    }
    {
        #CLASS {module_%1} {kill};
        #CLASS {module_%1} {open};
        #SHOWME \nReloading <169>%1<099>...;
        #READ modules/%1.tin;
        #CLASS {module_%1} {close};
        #DELAY {0} {#SHOWME Reload complete.\n};
    };
};

#FUNCTION {is_out_of_date}
{
    #INFO system save;
    #LOCAL tt_version {$info[SYSTEM][CLIENT_VERSION]};
    #UNVAR info;
    #REPLACE {tt_version} {.} {};
    #REPLACE {tt_version} {b} {};
    #IF {$tt_version < 20201}
    {
        #RETURN 1;
    }
    {
        #RETURN 0;
    };
}

#EVENT {PROGRAM START}
{
    #IF {@is_out_of_date{}}
    {
        #SHOWME <001>Your version of Tintin++ is out of date.;
        #SHOWME <001>Please upgrade to v2.02.01 or later.;
    }
    {
        #LINE quiet {#READ core_module.tin};
        newsess one;
    };
};

#ALIAS {newsess %1}
{
    #SESSION %1 ${mud_addr} ${mud_port};
};

#EVENT {SESSION CONNECTED}
{
    #LINE quiet {#READ core_module.tin};
    #IF {"$autologin" == "true"}
    {
        #ACTION {{^Enter your choice or the name of your character:}}
        {
            #SEND $login_name;
            #SEND $login_password;

            #UNVAR autologin;
            #UNVAR login_name;
            #UNVAR login_password;

            #UNACTION {{^Enter your choice or the name of your character:}};
        }
        {5};
    };
    #ELSEIF {"$autologin" == "password"}
    {
        #ACTION {{^Password:}}
        {
            #SEND $login_password;
            #UNACTION {{^Password:}};
        }
    };
    #ELSE
    {
        #UNVAR autologin;
        #UNVAR login_name;
        #UNVAR login_password;
    };
}

#EVENT {SCREEN RESIZE}
{
    @create_layout{};
    #IF {@has_module{map}}
    {
        @on_map_resize{};
    };
    #IF {@has_module{chat}}
    {
        @on_chat_resize{};
    };
};

#FUNCTION {listen}
{
    #ECHO %1 %2;
    #IF {"%1" == "" || "%2" == ""}
    {
        #ECHO {<119>Error subscribing to event. Event name and target function name must be provided.};
    }
    {
        #IF {&{events[%1]}}
        {
            #LIST events[%1] add %2;
        }
        {
            #LIST events[%1] create %2;
        }
    }
    #RETURN #NOP;
};

#FUNCTION {event}
{
    #IF {&{events[%1]}}
    {
        #FOREACH {$events[%1][%*]} {func}
        {
            #LINE sub var #LOCAL tmp {@${func}{}};
        };
    };
    #RETURN #NOP;
};

#VAR telnet[iac] \xFF;
#VAR telnet[do] \xFD;
#VAR telnet[sb] \xFA;
#VAR telnet[se] \xF0;
#VAR telnet[gmcp] \xC9;

#EVENT {IAC WILL GMCP}
{
    #SEND {$telnet[iac]$telnet[do]$telnet[gmcp]};
    #ECHO {<069>GMCP<099> enabled and supported.};
}

#ALIAS {gmcp %1 %*}
{
    #SEND $telnet[iac]$telnet[sb]$telnet[gmcp]%1 %2$telnet[iac]$telnet[se];
}

#ALIAS {gmcp %1}
{
    #SEND $telnet[iac]$telnet[sb]$telnet[gmcp]%1$telnet[iac]$telnet[se];
}

#EVENT {IAC SB GMCP logging.error IAC SE}
{
    #ECHO {<119>%0};
}
